dependencies {
    compile project(':main:ip')
    compile project(':main:io')
    compile project(':main:feature')
    compile project(':main:calibration')
    compile project(':main:geo')
    compile project(':main:sfm')
    compile project(':main:recognition')
    compile project(':main:visualize')
    compile project(':evaluation')
}

task createAppletTmp( dependsOn: jar ) {
    doFirst {
        // decompress the jars it depends on to remove duplicate files.  They will decompress on top of each other
        copy {
            from files(sourceSets.main.output.classesDir)
            from files(sourceSets.main.output.resourcesDir)
            from { configurations.compile.collect { zipTree(it) } }
            into 'tmp'
        }
    }
}

task appletJar( type: Jar , dependsOn: createAppletTmp ) {

    from files('tmp')

    manifest {
        attributes(
                "Permissions": "sandbox",
                "Codebase": "*.boofcv.org",
//                "Trusted-Only": "true", // doesn't work on windows if this is true
                "Caller-Allowable-Codebase": "boofcv.org 127.0.0.1",
                "Application-Name": "BoofCV Demonstration"
        )
    }

    destinationDir = file('./')
    archiveName = 'BoofCV_Applet.jar'

    doLast {
        file('tmp').deleteDir()
    }
}

idea {
    module {
        name = "BoofCV Applet"
    }
}